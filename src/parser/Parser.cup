package parser;

import java_cup.runtime.*;
import dctl.formulas.*;
import java.util.Set;
import java.util.HashSet;


parser code {:

	Set<StateFormula> _spec;

	public static Set<StateFormula> parse_specification(String file) throws Exception{
		if (file == null) 
			throw new Exception("The path to the file containing the specification must be supplied.");
		SymbolFactory sf = new DefaultSymbolFactory();
		Parser p = new Parser(new Scanner(new java.io.FileInputStream(file),sf),sf);
		p._spec = new HashSet<StateFormula>();
		p.parse();		
		return p._spec;
	}

	public static void main(String args[]) throws Exception {
		SymbolFactory sf = new DefaultSymbolFactory();
		if (args.length==0) new Parser(new Scanner(System.in,sf),sf).parse();
		else new Parser(new Scanner(new java.io.FileInputStream(args[0]),sf),sf).parse();
	}
:}

terminal String LPAREN, RPAREN, SEMI;
terminal String TRUE, FALSE, ID;
terminal String NEG, AND, OR, IMPLIES;
terminal String EXISTS, FORALL, PERMISSION, OBLIGATION;
terminal String NEXT, UNTIL, FUTURE, GLOBALLY;

non terminal specification;
non terminal StateFormula formula, state_form;
non terminal PathFormula path_form;

precedence left IMPLIES;
precedence left OR;
precedence left AND;
precedence left UNTIL;
precedence left FUTURE, GLOBALLY;
precedence right NEXT;
precedence right NEG;

specification ::= 	formula:f specification	{: parser._spec.add(f);System.out.println(":O " + parser._spec.contains(f)); :}
				|	formula:f				{: parser._spec.add(f);System.out.println(":O " + parser._spec.contains(f)); :}
			;

formula ::= state_form:s SEMI {: RESULT = s; :}
           ; 

state_form 	::=	TRUE									{: RESULT = new True();	:}
			|	FALSE									{: RESULT = new False(); :}
            | 	ID:i 									{: RESULT = new Proposition(i);  	:}
	    	| 	NEG state_form:p 						{: RESULT = new Negation(p);		:} 
	    	| 	state_form:p AND state_form:q 			{: RESULT = new And(p,q);	:}    
	    	| 	state_form:p OR state_form:q 			{: RESULT = new Or(p,q);	:}
	    	| 	state_form:p IMPLIES state_form:q 		{: RESULT = new Or(new Negation(p), q); :}
	    	|	LPAREN state_form:p RPAREN				{: RESULT = p; :}
            | 	EXISTS LPAREN path_form:p RPAREN 		{: RESULT = new Exists(p); :}
            | 	FORALL LPAREN path_form:p RPAREN 		{: RESULT = new Forall(p); :}
            | 	OBLIGATION LPAREN path_form:p RPAREN 	{: RESULT = new Obligation(p); :}
            | 	PERMISSION LPAREN path_form:p RPAREN	{: RESULT = new Permission(p); :}
	    ;

path_form ::=	NEXT state_form:p 						{: RESULT = new Next(p); 	:}
			|	FUTURE state_form:p 			 		{: RESULT = new Until(new True(), p); 	:}
			|	GLOBALLY state_form:p			 		{: RESULT = new WeakUntil(p, new False()); :}
			|	state_form:p UNTIL:u state_form:q 		{: RESULT = new Until(p, q); :}
		;
          
